buildscript {


    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.+'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'android-apt'

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

def buildTime() {
    return new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'
    defaultConfig {
        applicationId "com.roodie.materialmovies"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 3
        versionName "1.24"
        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true

        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    dexOptions {
        incremental true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            versionNameSuffix "-debug"
        }

        release {
            minifyEnabled false
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }

   /*
   Force usage libraries in the test, to avoid conflict dependencies
   More info about this tools bug: (http://stackoverflow.com/questions/33317555/conflict-with-dependency-com-android-supportsupport-annotations-resolved-ver)
   */
    configurations.all {
        resolutionStrategy.force 'com.android.support:support-annotations:23.1.1'
        resolutionStrategy.force 'com.android.support:appcompat-v7:23.1.1'
        resolutionStrategy.force 'com.android.support:design:23.1.1'
        resolutionStrategy.force 'com.android.support:recyclerview-v7:23.1.1'

    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

dependencies {
    compile project(':model')
    //support
    compile "com.android.support:appcompat-v7:$rootProject.ext.support_version"
    compile "com.android.support:support-annotations:$rootProject.ext.support_version"
    compile "com.android.support:recyclerview-v7:$rootProject.ext.support_version"
    compile "com.android.support:design:$rootProject.ext.support_version"
    compile "com.android.support:cardview-v7:$rootProject.ext.support_version"
    compile "com.android.support:palette-v7:$rootProject.ext.support_version"
    compile "com.android.support:percent:$rootProject.ext.support_version"
    compile "com.android.support:gridlayout-v7:$rootProject.ext.support_version"
    //moxy
    compile "com.arello-mobile:moxy:$rootProject.ext.moxy_version"
    compile "com.arello-mobile:moxy-android:$rootProject.ext.moxy_version"
    apt "com.arello-mobile:moxy-compiler:$rootProject.ext.moxy_version"
    //third
    compile 'com.hkm.ui:ultimaterecyclerview:0.5.8'
    compile 'com.github.asyl.animation:arcanimator:1.0.0'
    compile 'com.github.ozodrukh:CircularReveal:1.0.4@aar'
    compile 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    compile 'com.github.castorflex.smoothprogressbar:library-circular:1.1.0'
    compile 'com.github.chrisbanes.photoview:library:1.2.+'
    compile('com.github.afollestad.material-dialogs:core:0.8.5.7@aar') {
        transitive = true
    }
    //picasso
    compile 'com.squareup.picasso:picasso:2.5.2'
    //butterknife
    compile 'com.jakewharton:butterknife:6.1.0'
    //dagger
    apt 'com.squareup.dagger:dagger-compiler:1.2.+'
    //crash
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
    compile('com.crashlytics.sdk.android:answers:1.3.6@aar') {
        transitive = true;
    }
    // Dependencies for local unit tests
    testCompile "junit:junit:$rootProject.ext.junit_version"
    testCompile "org.powermock:powermock-module-junit4:$rootProject.ext.powermock_version"
    testCompile "org.powermock:powermock-api-mockito:$rootProject.ext.powermock_version"

    testCompile "org.mockito:mockito-all:$rootProject.ext.mockito_version"
    testCompile "org.hamcrest:hamcrest-all:$rootProject.ext.hamcrest_version"

    // Android Testing Support Library's runner and rules
    androidTestCompile "com.android.support.test:runner:$rootProject.ext.runnerVersion"
    androidTestCompile "com.android.support.test:rules:$rootProject.ext.rulesVersion"

    // Espresso UI Testing dependencies.
    androidTestCompile "com.android.support.test.espresso:espresso-core:$rootProject.ext.espresso_version"
    androidTestCompile ("com.android.support.test.espresso:espresso-contrib:$rootProject.ext.espresso_version") {
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'recyclerview-v7'
    }
    compile "com.android.support.test.espresso:espresso-idling-resource:$rootProject.ext.espresso_version"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$rootProject.ext.espresso_version"

    compile fileTree(include: ['*.jar'], dir: 'libs')
}
